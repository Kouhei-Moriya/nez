// XML Document Type Declaration
// author Shinya Yamaguchi

TopLevel
    = Document
    
Chunk 
    = Document
    
File 
    = Document

Document
    = { (PROLOG)? (MISC* (@Dtd)+  MISC* )+ #Doc}
    
    
PROLOG
    =  '<?xml' (_* NAME '=' STRING)*  _* '?>' 
//DocType
//    = '<!DOCTYPE' _* {@DocTypeName '[' (@Dtd)* #doctype } ']>'
    

Dtd
    = ( ( '<!' (element/attlist/entity/condsect) '>') / MISC)  _*

IGNORESECT
	= ( '<!' (![>] .)* '>' _*)/ (_* '%' NAME ';' _*) / ( (!']' .)+) / MISC

condsect
	= IGNORE / include
	
include	
	= '[' _* ('INCLUDE'/NAME) _* '[' _* (@Dtd)* _*  NAME? _* MISC*  ']]' _*
	
IGNORE
	= '[' _* 'IGNORE' _* '[' _* IGNORESECT* _* ']]' _*

element
	= 'ELEMENT' _* {@Name _* contentSpec _* #Element} _*

contentSpec
	=  @SpContent / topMember 
	
SpContent
	= {'EMPTY' #Empty} / {'ANY' #Any}

topMember
	=   ('(' _* @Data _* ')' {@ &'' addQualifier}? ) / @Name / @Choice / @Seq
	
member
	=   (@Data / @Name / @Choice / @Seq) 

Choice 
	= { '(' _* member (_* OR _* member)+ _* ')'  #Choice}{@ &'' addQualifier}?

Seq 
	= { '(' _* member (_* VS _* member)* _* ')' #Seq} {@ &'' addQualifier}?

addQualifier
	= 	'+' #OneMore
		/ '*' #ZeroMore
		/ '?' #Option 

Data
	= { '#PCDATA' #Data}
	

	
OR
	= _* '|' _* 
	

attlist
	= 'ATTLIST' _* {@Name _* (@AttDef)* #Attlist} _*

AttDef
	= { @Name _* @AttType _* @DefaultDecl _* #Def}
	 
AttType
	= { (@{'CDATA' #CDATA} / tokenized / @Enumerated) #Type }

ATTVAL
	= (![<&"].)*

tokenized
	= 
		  @{'IDREFS' #IDREFS}		
		/ @{'IDREF' #IDREF}
		/ @{'ID' #ID}  
		/ @{'ENTITY' #ENTITY} 
		/ @{'ENTITIES' #ENTITIES} 
		/ @{'NMTOKENS' #NMTOKENS} 
		/ @{'NMTOKEN' #NMTOKEN} 

Enumerated
	= { '(' _* @{ [-A-Za-z0-9:._%;]* #enumMember } _* ( OR  @{ [-A-Za-z0-9:._%;]* #EnumMember } )*   _* ')'  _* #Enum }
	
DefaultDecl
	= { ( @{'#REQUIRED' #REQUIRED} / @{'#IMPLIED' #IMPLIED} / ( (@{'#FIXED' #FIXED})? _* @DefaultValue )) #Default}
	
DefaultValue
	= ('"'{ (![<&"].)*#Value} '"') / ('\''{ (![<&"].)*#Value} '\'')
	

entity
	= 'ENTITY' _*  (gedecl/pedecl) _*

gedecl
	= { @Name _* entitydef _* #Entity}

pedecl
	= { '%' _* @Name _* PEDEF _* #Pedecl}

entitydef
	=  @EntityValue / (EXID NDATADECL?)

PEDEF
	= STRING / EXID

EntityValue
	= {'"' (!["].)* '"'#Value}

EXID 
	= ('SYSTEM' _* SLITERAL)
		/ ('PUBLIC' _* PUBIDLITERAL _* SLITERAL)

NDATADECL
	= 'NDATA' _* NAME
	
SLITERAL
	= (  '\'' ( !'\'' . )* '\'') / ( '"' ( !'"' . )* '"')

PUBIDLITERAL
	= '"' PUBLICCHAR* '"'
	
PUBLICCHAR
	=  [a-zA-Z0-9] / [-'()+,./:=?;!*#@$_% ]  / [\xD] / [\xA] 


Name
	= { [A-Za-z:%_] ([-A-Za-z0-9:._%;])* #Name } {@ &'' addQualifier}?

NAME
	=  [A-Za-z:%_] ([-A-Za-z0-9:._%;])*

String
    =  '"' { ( !'"' . )*  #String } '"'
    
STRING
	=  '"' ( !'"' . )* '"'
    


//Spacing etc


MISC
    = COMMENT / _+
    

COMMENT
    = '<!--' (!'-->' . )* '-->'
    
    
VS
	= _* ',' _*
	

_ = [ \t\r\n]
